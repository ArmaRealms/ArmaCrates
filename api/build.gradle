//file:noinspection GroovyAssignabilityCheck
plugins {
    id 'maven-publish'
}

project.group = "us.crazycrew.crazycrates"
project.version = "0.3"

base {
    archivesName = ("${rootProject.name}-${project.name}")
}

dependencies {
    compileOnlyApi(libs.adventure)
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    repositories {
        maven {
            url("https://repo.crazycrew.us/releases")

            credentials {
                username = System.getenv("GRADLE_USERNAME")
                password = System.getenv("GRADLE_PASSWORD")
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = "api"

            from components.java

            pom {
                name = "CrazyCrates API"
                description = "A nifty crates plugin for Minecraft Servers."
                url = "https://github.com/Crazy-Crew/CrazyCrates"

                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/Crazy-Crew/CrazyCrates/blob/main/LICENSE"
                    }
                }

                developers {
                    developer {
                        id = "ryderbelserion"
                        name = "Ryder Belserion"
                        url = "https://github.com/ryderbelserion"
                        email = "no-reply@ryderbelserion.com"
                    }

                    developer {
                        id = "badbones69"
                        name = "Badbones69"
                        url = "https://github.com/badbones69"
                        email = "joewojcik14@gmail.com"
                    }
                }

                scm {
                    connection = "scm:git:https://github.com/Crazy-Crew/CrazyCrates.git"
                    developerConnection = "scm:git:git@github.com:Crazy-Crew/CrazyCrates.git"
                    url = "https://github.com/Crazy-Crew/CrazyCrates"
                }

                issueManagement {
                    system = "Github"
                    url = "https://github.com/Crazy-Crew/CrazyCrates/issues"
                }
            }
        }
    }
}